(ns hyperfiddle.ide.fiddles.topnav
  (:require
    [cats.monad.either :as either]
    [clojure.string :as string]
    [contrib.reactive :as r]
    [contrib.ui :refer [easy-checkbox]]
    [contrib.ui.tooltip :refer [tooltip]]
    [hypercrud.browser.context :as context]
    [hyperfiddle.actions :as actions]
    [hyperfiddle.data]
    [hyperfiddle.domain :as domain]
    [hyperfiddle.foundation :as foundation]
    [hyperfiddle.runtime :as runtime]
    [hyperfiddle.security.client :as security]
    [hyperfiddle.ui :as ui :refer [markdown]]))


(defn any-loading? [peer]
  (some (comp not nil? :hydrate-id val) @(runtime/state peer [::runtime/partitions])))

(defn loading-spinner [ctx & [?class]]
  (if @(r/track any-loading? (:peer ctx))
    [:div.display-inline-flex [re-com.core/throbber]]))

(defn renderer' [val ctx props]
  (let [target-route (context/target-route ctx)]
    [:div props
     [:div.left-nav
      [tooltip {:label "Home"} [:a {:href "/"} @(runtime/state (:peer ctx) [::runtime/domain :domain/ident])]]
      (let [fiddle-ident (first target-route)
            ; domain editor doesn't target a fiddle at all, and some fiddles are named by uuid
            fiddle-name (if (keyword? fiddle-ident) (name fiddle-ident))]
        [tooltip {:label fiddle-name}
         [:span fiddle-name]])
      [ui/link :hyperfiddle.ide/entry-point-fiddles ctx "index"
       {:tooltip [nil "Fiddles in this domain"]
        :iframe-as-popover true}]]

     [:div.right-nav {:key "right-nav"}                     ; CAREFUL; this key prevents popover flickering
      [loading-spinner ctx]
      (let [tooltip [:div {:style {:text-align "left"}}
                     [markdown
                      (->> @(runtime/state (:peer ctx) [::runtime/domain :domain/databases])
                           (reduce (fn [acc {:keys [:domain.database/name :domain.database/record]}]
                                     (if (contains? acc (:database/uri record))
                                       (update acc (:database/uri record) conj name)
                                       (assoc acc (:database/uri record) [name])))
                                   {@(runtime/state (:peer ctx) [::runtime/domain :domain/fiddle-database :database/uri]) ["Source"]})
                           (map (fn [[uri dbnames]]
                                  (let [prefix (if @(runtime/state (:peer ctx) [::runtime/auto-transact uri])
                                                 "- [x] "
                                                 "- [ ] ")]
                                    (str prefix (string/join "/" dbnames) " (" uri ")"))))
                           (string/join "\n")
                           (str "##### Auto-transact:\n\n"))
                      {:hyperfiddle.ui.markdown-extensions/unp true}]]
            props {:tooltip [nil tooltip]
                   :class (when-not @(r/fmap empty? (runtime/state (:peer ctx) [::runtime/partitions nil :stage]))
                            "stage-dirty")
                   :iframe-as-popover true}]
        [ui/link :hyperfiddle.ide/stage ctx "stage" props])
      (either/branch
        (hyperfiddle.data/browse+ ctx :hyperfiddle/topnav-new) ; iframe wrapper for naked qfind color tag
        #(vector :span %)
        (fn [ctx]
          (ui/link :hyperfiddle.ide/new-fiddle ctx "new" (let [disabled? (not (security/can-create? ctx)) ; we explicitly know the context here is $
                                                               anonymous? (nil? @(runtime/state (:peer ctx) [::runtime/user-id]))]
                                                           {:disabled disabled?
                                                            :tooltip (cond
                                                                       (and anonymous? disabled?) [:warning "Please login"]
                                                                       disabled? [:warning "Writes restricted"])
                                                            :hyperfiddle.ui.popover/redirect (fn [popover-data]
                                                                                               [(:fiddle/ident popover-data)])}))))
      [tooltip {:label "Environment administration"} (ui/link :hyperfiddle.ide/domain ctx "env")]
      (if @(runtime/state (:peer ctx) [::runtime/user-id])
        (if-let [{:keys [:hypercrud.browser/result]} (hyperfiddle.data/browse ctx :hyperfiddle.ide/account)]
          (let [props {:tooltip [nil @(r/fmap :user/email result)]
                       :iframe-as-popover true}]
            [ui/link :hyperfiddle.ide/account ctx @(r/fmap :user/name result) props]))
        [:a {:href (foundation/stateless-login-url ctx)} "login"])]]))

(defn hack-login-renderer [val ctx props]
  [:div props
   [:div.left-nav
    [tooltip {:label "Home"} [:a @(runtime/state (:peer ctx) [::runtime/domain :domain/ident])]]]
   [:div.right-nav {:key "right-nav"}                       ; CAREFUL; this key prevents popover flickering
    [loading-spinner ctx]]])

(defn renderer [val ctx props]
  (let [#_#_ctx (hypercrud.browser.context/element ctx)               ; its blank
        f (if (and (= :hyperfiddle.ide/please-login (first (context/target-route ctx)))
                   (not= [:domain/ident foundation/source-domain-ident] (:domain-eid (runtime/host-env (:peer ctx)))))
            hack-login-renderer
            renderer')]
    [f val ctx props]))

(letfn [(toggle-auto-transact! [ctx selected-uri]
          (runtime/dispatch! (:peer ctx) [:toggle-auto-transact @selected-uri]))]
  (defn ^:export stage-ui-buttons [selected-uri stage ctx]
    (let [writes-allowed?+ (let [hf-db (domain/uri->hfdb @selected-uri @(runtime/state (:peer ctx) [::runtime/domain]))
                                 subject @(runtime/state (:peer ctx) [::runtime/user-id])
                                 user @(runtime/state (:peer ctx) [::runtime/user])]
                             (security/subject-can-transact? hf-db subject user))
          anonymous? (nil? @(runtime/state (:peer ctx) [::runtime/user-id]))]
      [:<>
       [tooltip (either/branch
                  writes-allowed?+
                  (fn [e] {:status :warning :label "Misconfigured db security"})
                  (fn [writes-allowed?]
                    (cond (and (not writes-allowed?) anonymous?) {:status :warning :label "Please login"}
                          (not writes-allowed?) {:status :warning :label "Writes restricted"}
                          (empty? @stage) {:status :warning :label "no changes"})))
        (let [disabled? (either/branch
                          writes-allowed?+
                          (constantly true)
                          (fn [writes-allowed?] (or (not writes-allowed?) (empty? @stage))))]
          [:button {:disabled disabled?
                    :style (if disabled? {:pointer-events "none"})
                    :on-click (fn []
                                (let [nil-branch-aux {:hyperfiddle.ide/foo "page"}
                                      action (actions/manual-transact-uri! (:peer ctx) nil-branch-aux @selected-uri)]
                                  (runtime/dispatch! (:peer ctx) action)))}
           "transact!"])]
       " "
       #_[tooltip (either/branch
                  writes-allowed?+
                  (fn [e] {:status :warning :label "Misconfigured db security"})
                  (fn [writes-allowed?]
                    (cond (and anonymous? (not writes-allowed?)) {:status :warning :label "Please login"}
                          (not writes-allowed?) {:status :warning :label "Writes restricted"}
                          (not (empty? @stage)) {:status :warning :label "please transact! all changes first"})))
        (let [is-disabled (either/branch
                            writes-allowed?+
                            (constantly true)
                            (fn [writes-allowed?]
                              (or (not writes-allowed?) (not (empty? @stage)))))
              is-auto-transact @(runtime/state (:peer ctx) [::runtime/auto-transact @selected-uri])]
          [easy-checkbox {:disabled is-disabled
                          :style (if is-disabled {:pointer-events "none"})
                          :checked (boolean is-auto-transact)
                          :on-change (r/partial toggle-auto-transact! ctx selected-uri)}
           "auto-transact"])]])))
